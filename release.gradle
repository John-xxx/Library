// 打包某个库到JCenter
// ..\gradlew.bat clean build assemble bintrayUpload -PbintrayUser=Liux -PbintrayKey=xxxxx -PdryRun=false
// 打包某个库到本地
// ..\gradlew.bat clean build assemble publishToMavenLocal
// 打包某个库到私服
// ..\gradlew.bat clean build assemble uploadArchives

def params = project.ext.params

// release to JCenter
apply plugin: 'com.novoda.bintray-release'

publish {
    userOrg = 'lx0758'
    repoName = 'maven'

    groupId = 'com.liux'
    artifactId = params.artifactId
    publishVersion = params.version

    licences = ['MIT']

    desc = 'My rapid development library'
    website = 'https://gitee.com/lx0758/Library'
    repository = 'https://gitee.com/lx0758/Library.git'
}

// release to Nexus
apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: 'http://maven.lx0758.cc/artifactory/android/') {
                Properties properties = new Properties()
                InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
                properties.load(inputStream)
                def uploadName = properties.getProperty('uploadUser')
                def uploadPassword = properties.getProperty('uploadPassword')
                inputStream.close()

                authentication(userName:uploadName, password:uploadPassword)
            }
            pom.project {
                groupId = 'com.liux'
                artifactId = params.artifactId
                version = params.version

                url 'https://gitee.com/lx0758/Library'
                name project.name
                packaging 'aar'
                description 'My rapid development library'

                scm {
                    url 'https://gitee.com/lx0758/Library'
                    connection 'git@gitee.com:lx0758/Library.git'
                    developerConnection 'git@gitee.com:lx0758/Library.git'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://opensource.org/licenses/mit'
                        distribution 'Liux'
                    }
                }

                developers {
                    developer {
                        id 'lx0758'
                        name 'Liux'
                        email 'lx0758@qq.com'
                    }
                }
            }
        }
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
}